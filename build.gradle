plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.springframework.boot' version "3.4.1"
}

version '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.4.1')
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.seleniumhq.selenium:selenium-java:3.6.0'
    implementation 'com.google.code.maven-play-plugin.net.sf.jtidy:jtidy:r938'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation platform('org.apache.groovy:groovy-bom:4.0.24')
    testImplementation 'org.apache.groovy:groovy'
    testImplementation platform('org.spockframework:spock-bom:2.4-M5-groovy-4.0')
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation 'org.spockframework:spock-spring'
}

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

tasks.register("clear") {
    dependsOn 'clean'
    doLast {
        delete 'dist'
    }
}

test {
    finalizedBy 'jacocoTestReport'
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-parameters'
}

tasks.register('install') {
    dependsOn 'build'
    group 'build'
    description 'prepare dist folder for deployment'
    doFirst {
        println '>>>mkdir dist'
        mkdir 'dist/lib'
        mkdir 'dist/bin'
        println '>>>mkdir dist/export'
        mkdir 'dist/export'
        mkdir 'dist/export/static'
    }

    doLast {
        println '>>>copy meta files'
        copy {
            from('.') {
                include 'logo.png'
                include 'app_meta.properties'
            }
            into './dist'
        }

        println '>>>copy bin and export'
        copy {
            from('.') {
                include 'export/**'
            }
            into 'dist'
        }
        println '>>>copy bin'
        copy {
            from 'scripts'
            into 'dist/bin'
        }
        println '>>>copy libs'
        copy {
            from('build/libs') {
                exclude 'eggfund*plain.jar'
            }
            into 'dist/lib'
        }
    }
}

